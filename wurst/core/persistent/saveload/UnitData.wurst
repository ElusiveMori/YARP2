package UnitData

import YARPPrelude
import initlater YUnit
import Registry
import initlater Permissions
import Attachment

constant CUSTOM_HERO_NAME_FLAG = 0
constant CUSTOM_NAME_FLAG = 1
constant CUSTOM_MAX_LIFE_FLAG = 2
constant CUSTOM_MAX_MANA_FLAG = 3
constant CUSTOM_DAMAGE_FLAG = 4
constant CUSTOM_ATTACK_COOLDOWN_FLAG = 5
constant CUSTOM_FLY_HEIGHT_FLAG = 6
constant CUSTOM_TINT_FLAG = 7
constant SHORT_DESCRIPTION_FLAG = 8
constant IS_UPROOTED_FLAG = 9

class UnitAttachmentData
    string attachmentId
    string attachmentPoint
    int color
    colorA tint

    construct(string attachmentId, string attachmentPoint, int color, colorA tint)
        this.attachmentId = attachmentId
        this.attachmentPoint = attachmentPoint
        this.color = color
        this.tint = tint

class SingleUnitData implements DataSerializable
    private yunit what
    private vec2 origin
    private vec2 pos
    private int dictionaryId

    private bitset flags

    private string customHeroName
    private string customName
    private string shortDescription

    private int playerColorId
    private colorA tint

    private real size
    private real flyHeight

    private int maxMana
    private int maxLife

    private int damage
    private real attackCooldown

    private LinkedList<UnitAttachmentData> attachments

    ondestroy
        for attachment in attachments
            destroy attachment
        destroy attachments

    private function serializeMandatory(DataWriter writer)
        writer.writeShort(dictionaryId)
        writer.writeReal(what.u.getX() - origin.x)
        writer.writeReal(what.u.getY() - origin.y)

        writer.writeByte(what.getColor().toInt())
        writer.writeReal(what.getScale())

        writer.writeInt(what.getAttachments().size())
        for attachment in what.getAttachments()
            writer.writeString(attachment.getAttachmentId())
            writer.writeString(attachment.getAttachmentPoint())
            writer.writeByte(attachment.getColor().toInt())
            writer.writeByte(attachment.getTint().red)
            writer.writeByte(attachment.getTint().green)
            writer.writeByte(attachment.getTint().blue)
            writer.writeByte(attachment.getTint().alpha)

    private function serializeOptionals(DataWriter writer)
        var flags = bitset(0)
        if what.hasCustomName()
            flags = flags.add(CUSTOM_NAME_FLAG)
        
        if what.hasCustomHeroName()
            flags = flags.add(CUSTOM_HERO_NAME_FLAG)

        if what.hasCustomMaxLife()
            flags = flags.add(CUSTOM_MAX_LIFE_FLAG)

        if what.hasCustomMaxMana()
            flags = flags.add(CUSTOM_MAX_MANA_FLAG)

        if what.hasCustomDamage()
            flags = flags.add(CUSTOM_DAMAGE_FLAG)

        if what.hasCustomAttackCooldown()
            flags = flags.add(CUSTOM_ATTACK_COOLDOWN_FLAG)

        if what.hasCustomFlyHeight()
            flags = flags.add(CUSTOM_FLY_HEIGHT_FLAG)

        if what.hasCustomTint()
            flags = flags.add(CUSTOM_TINT_FLAG)

        if what.hasShortDesc()
            flags = flags.add(SHORT_DESCRIPTION_FLAG)

        if what.isUprooted()
            flags = flags.add(IS_UPROOTED_FLAG)

        writer.writeInt(flags.val)

        if what.hasCustomName()
            writer.writeString(what.getName())
        
        if what.hasCustomHeroName()
            writer.writeString(what.getHeroName())

        if what.hasCustomMaxLife()
            writer.writeInt(what.getMaxLife().toInt())

        if what.hasCustomMaxMana()
            writer.writeInt(what.getMaxMana().toInt())

        if what.hasCustomDamage()
            writer.writeInt(what.getDamage())

        if what.hasCustomAttackCooldown()
            writer.writeReal(what.getAttackCooldown())

        if what.hasCustomFlyHeight()
            writer.writeReal(what.getHeight())

        if what.hasCustomTint()
            let tint = what.getTint()
            writer.writeByte(tint.red)
            writer.writeByte(tint.green)
            writer.writeByte(tint.blue)
            writer.writeByte(tint.alpha)

        if what.hasShortDesc()
            writer.writeString(what.getShortDesc())

    override function serialize(DataWriter writer)
        serializeMandatory(writer)
        serializeOptionals(writer)

    private function deserializeMandatory(DataReader reader)
        dictionaryId = reader.readShort()
        pos = vec2(reader.readReal(), reader.readReal())
        playerColorId = reader.readByte()
        size = reader.readReal()

        attachments = new LinkedList<UnitAttachmentData>
        let attachmentsSize = reader.readInt()
        for i = 0 to attachmentsSize - 1
            let attachmentId = reader.readString()
            let attachmentPoint = reader.readString()
            let color = reader.readByte()
            let r = reader.readByte()
            let g = reader.readByte()
            let b = reader.readByte()
            let a = reader.readByte()
            let tint = colorA(r, g, b, a)
            attachments.push(new UnitAttachmentData(attachmentId, attachmentPoint, color, tint))

    private function deserializeOptionals(DataReader reader)
        let flags = bitset(reader.readInt())

        if flags.contains(CUSTOM_NAME_FLAG)
            customName = reader.readString()
        
        if flags.contains(CUSTOM_HERO_NAME_FLAG)
            customHeroName = reader.readString()

        if flags.contains(CUSTOM_MAX_LIFE_FLAG)
            maxLife = reader.readInt()

        if flags.contains(CUSTOM_MAX_MANA_FLAG)
            maxMana = reader.readInt()

        if flags.contains(CUSTOM_DAMAGE_FLAG)
            damage = reader.readInt()

        if flags.contains(CUSTOM_ATTACK_COOLDOWN_FLAG)
            attackCooldown = reader.readReal()

        if flags.contains(CUSTOM_FLY_HEIGHT_FLAG)
            flyHeight = reader.readReal()

        if flags.contains(CUSTOM_TINT_FLAG)
            tint = colorA(
                reader.readByte(),
                reader.readByte(),
                reader.readByte(),
                reader.readByte()
            )

        if flags.contains(SHORT_DESCRIPTION_FLAG)
            shortDescription = reader.readString()

        this.flags = flags

    override function deserialize(DataReader reader)
        deserializeMandatory(reader)
        deserializeOptionals(reader)

    function apply(rect area, player owner, UnitDictionary dictionary)
        let id = getIdForUid(dictionary.getUidForId(this.dictionaryId))
        let what = yunit(createUnit(owner, id, vec2(0, 0), angle(0)))

        what.setColor(playerColorId.toPlayerColor())
        what.setScale(size)

        for attachmentData in attachments
            let attachment = new UnitAttachment(what.u, attachmentData.attachmentId, attachmentData.attachmentPoint)
            attachment.setColor(attachmentData.color.toPlayerColor())
            attachment.setTint(attachmentData.tint)
            what.addAttachment(attachment)

        if flags.contains(CUSTOM_NAME_FLAG)
            what.setName(customName)
        
        if flags.contains(CUSTOM_HERO_NAME_FLAG)
            what.setHeroName(customHeroName)

        if flags.contains(CUSTOM_MAX_LIFE_FLAG)
            what.setMaxLife(maxLife)

        if flags.contains(CUSTOM_MAX_MANA_FLAG)
            what.setMaxMana(maxMana)

        if flags.contains(CUSTOM_DAMAGE_FLAG)
            what.setDamage(damage)

        if flags.contains(CUSTOM_ATTACK_COOLDOWN_FLAG)
            what.setAttackCooldown(attackCooldown)

        if flags.contains(CUSTOM_FLY_HEIGHT_FLAG)
            what.setHeight(flyHeight)

        if flags.contains(CUSTOM_TINT_FLAG)
            what.setTint(tint)

        if flags.contains(SHORT_DESCRIPTION_FLAG)
            what.setShortDesc(shortDescription)

        if flags.contains(IS_UPROOTED_FLAG)
            what.uproot()

        what.u.setXY(area.getCenter() + pos)

    function scan(rect area, yunit what, UnitDictionary dictionary)
        this.what = what
        this.origin = area.getCenter()
        this.dictionaryId = dictionary.getIdForUid(what.getUid())

class UnitDictionary implements DataSerializable
    private int currentId = 0
    private Table dictionary = new Table

    ondestroy
        destroy dictionary

    override function serialize(DataWriter writer)
        writer.writeShort(currentId)

        for i = 1 to currentId
            let uid = dictionary.loadString(i - 1)
            writer.writeString(uid)

    override function deserialize(DataReader reader)
        let size = reader.readShort()

        for i = 1 to size
            let uid = reader.readString()

            dictionary.saveString(i - 1, uid)
            dictionary.saveInt(uid.getHash(), i - 1)

    function getIdForUid(string uid) returns int
        let hash = uid.getHash()
        if dictionary.hasInt(hash)
            return dictionary.loadInt(hash)
        else
            dictionary.saveInt(hash, currentId)
            dictionary.saveString(currentId, uid)
            currentId++
            return currentId - 1

    function getUidForId(int id) returns string
        return dictionary.loadString(id)

public class MultipleUnitData implements DataSerializable
    private let list = new HashList<SingleUnitData>
    private let dictionary = new UnitDictionary()

    override function serialize(DataWriter writer)
        writer.writeInt(list.size())

        for data in list
            data.serialize(writer)

        dictionary.serialize(writer)

    override function deserialize(DataReader reader)
        let size = reader.readInt()

        for i = 0 to size - 1
            let data = new SingleUnitData
            data.deserialize(reader)
            list.add(data)

        dictionary.deserialize(reader)

    function apply(rect area, player owner)
        for unitData in list
            unitData.apply(area, owner, dictionary)

    function scan(rect area)
        forUnitsInRect(area) (what) ->
            if what.isModifiableBy(what.getOwner())
                let data = new SingleUnitData()
                data.scan(area, yunit(what), dictionary)
                list.add(data)